apply plugin: "maven-publish"
apply plugin: "fabric-loom"
apply plugin: "io.github.juuxel.loom-quiltflower"
apply plugin: "com.replaymod.preprocess"
apply plugin: "me.fallenbreath.yamlang"
apply plugin: 'com.modrinth.minotaur'
apply plugin: "com.github.breadmoirai.github-release"

int mcVersion = 1

preprocess {
    mcVersion = vars.get()["MC"] as int
    tabIndentation = false
}

repositories {
    maven { url "https://jitpack.io" }
    maven {
        name = "Curseforge"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven {
        name = "Gradle Plugin Portal"
        url = "https://plugins.gradle.org/m2/"
    }
}

// https://github.com/FabricMC/fabric-loader/issues/783
configurations {
    modRuntimeOnly.exclude group: "net.fabricmc", module: "fabric-loader"
}

dependencies {
    // loom
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // dependencies
    modImplementation fabricApi.module("fabric-command-api-${mcVersion > 11802 ? "v2" : "v1"}", project.fabric_api_version)

    // mixin extensions
// 	include api(annotationProcessor("com.github.LlamaLad7.mixinextras:mixinextras-fabric:${project.mixin_extras_version}"))
}

loom {
    runConfigs.all {
        // to make sure it generates all "Minecraft Client (:subproject_name)" applications
        ideConfigGenerated = true
        runDir "../../run"
        vmArgs "-Dmixin.debug.export=true"
    }
}

sourceSets {
    test {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

loom {
    runs {
        testmodClient {
            client()
            name = "Testmod Client"
            source sourceSets.test
        }
        testmodServer {
            server()
            name = "Testmod Server"
            source sourceSets.test
        }
    }
}

remapJar {
    remapperIsolation = true
}

if (mcVersion >= 11800) {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
} else if (mcVersion >= 11700) {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
} else {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

archivesBaseName = project.archives_base_name + "-" + project.mod_version + "+" + project.game_versions

processResources {
    inputs.property "version", project.mod_version

    filesMatching("fabric.mod.json") {
        def valueMap = [
                "id"                  : project.mod_id,
                "name"                : project.mod_name,
                "version"             : project.mod_version,
                "minecraft_dependency": project.minecraft_dependency,
        ]
        expand valueMap
    }
}

processTestResources {
    inputs.property "version", project.mod_version

    filesMatching("fabric.mod.json") {
        def valueMap = [
                "version"             : project.mod_version,
                "minecraft_dependency": project.minecraft_dependency,
        ]
        expand valueMap
    }
}

yamlang {
    targetSourceSets = [sourceSets.main]
    inputDir = "assets/${project.mod_id}/lang"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

java {
    withSourcesJar()
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

tasks.register("release") {
    group = "publishing"

    dependsOn "modrinth"
    dependsOn "githubRelease"
}

modrinth {
    token = env.fetchOrNull("MODRINTH_TOKEN")
    projectId = project.mod_id
    versionName = project.mod_name + " " + project.mod_version + " for " + project.game_versions
    versionNumber = project.mod_version
    versionType = "beta"
    uploadFile = remapJar
    additionalFiles = [sourcesJar]
    gameVersions = project.modrinth_versions.split(" ").toList()
    loaders = ["fabric", "quilt"]
    dependencies {
        required.project "fabric-api"
    }
    syncBodyFrom = rootProject.file("README.md").text
    changelog = rootProject.file("CHANGELOG.md").text
    failSilently = true
    debugMode = false // safety, disable to actually publish
}

githubRelease {
    token = env.fetch("GITHUB_TOKEN", "")
    owner = project.repo_owner
    repo = project.mod_id
    tagName = "${project.mod_version}+${project.game_versions}"
    targetCommitish = "multi"
    releaseName = project.mod_name + " " + project.mod_version + " for " + project.game_versions
    body = rootProject.file("CHANGELOG.md").text
    releaseAssets = [remapJar, sourcesJar]
    prerelease = true
    allowUploadToExisting = true // allows publishing multiple files
    dryRun = false // safety, disable to actually publish
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId archivesBaseName

            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
